SILENT

INCLUDE "%MOD_FOLDER%/lib/components/IA_rebalancing/helper_functions.tph"

// Add condition for checking specific kit in splprot.2da

LAF ~S!ADD_SPLPROT_2DA_ENTRY~
  STR_VAR
    condition_description = ~KIT=n~
    stat                  = ~152~
    value                 = ~-1~
    relation              = ~1~
  RET
    kit_equals_row = new_line_number
END

// Add proper CLAB file if it does not exist

ACTION_FOR_EACH mage_kit_clab_file IN ~CLABMA01.2da~ ~CLABMA02.2da~ ~CLABMA03.2da~ ~CLABMA04.2da~ ~CLABMA05.2da~ ~CLABMA06.2da~ ~CLABMA07.2da~ ~CLABMA08.2da~ ~CLABMA09.2da~ BEGIN
  ACTION_IF NOT FILE_EXISTS_IN_GAME ~%mage_kit_clab_file%~ THEN BEGIN
    COPY ~IATweaks\resources\2da\clabtemp.2da~ ~override/%mage_kit_clab_file%~
  END
END

// Web

COPY_EXISTING ~spwi215.spl~ ~override~
  SAY UNIDENTIFIED_DESC @6056
  READ_BYTE 0x1f spell_flags
  WRITE_BYTE 0x1f (%spell_flags% BOR 0b00000001) // disable the spell for Diviners
  WRITE_BYTE 0x1f (%spell_flags% BAND 0b11111101) // enable the spell for Conjurers
  WRITE_SHORT 0x22 14 // Casting animation
  WRITE_BYTE 0x27 2 // School

// RVE

COPY_EXISTING ~spwi905.spl~ ~override~
  // Remove removal of Illusion spells from party
  LPF ~DELETE_EFFECT~
    INT_VAR
      match_opcode = 47
  END
  LPF ~DELETE_EFFECT~
    INT_VAR
      match_opcode = 221
  END

//////// CONJURER //////////

// Create more powerful versions of summons for Conjurer

ACTION_DEFINE_ASSOCIATIVE_ARRAY conjurer_new_summons_mapping BEGIN
  "s!minosu"=>"s!monsu4"
  "s!salasu"=>"s!monsu5"
  "s!trolsu"=>"s!monsu6"
  "s!yuansu"=>"s!monsu7"
END

ACTION_PHP_EACH conjurer_new_summons_mapping AS orig_cre=>new_cre BEGIN
  COPY_EXISTING ~%orig_cre%.cre~ ~override/%new_cre%.cre~
    LPF ~ADD_CRE_EFFECT~ // +20% HP
      INT_VAR
        opcode = 18
        target = 1
        power = 0
        parameter1 = 120
        parameter2 = 2
        timing = 9
        resist_dispel = 0
    END
    LPF ~ADD_CRE_EFFECT~ // +20% HP
      INT_VAR
        opcode = 17
        target = 1
        power = 0
        parameter1 = 120
        parameter2 = 2
        timing = 9
        resist_dispel = 0
    END
    LPF ~ADD_CRE_EFFECT~ // + 2 AC
      INT_VAR
        opcode = 0
        target = 1
        power = 0
        parameter1 = 2
        parameter2 = 0
        timing = 9
        resist_dispel = 0
    END
    LPF ~ADD_CRE_EFFECT~ // + 2 Str
      INT_VAR
        opcode = 44
        target = 1
        power = 0
        parameter1 = 2
        parameter2 = 0
        timing = 9
        resist_dispel = 0
    END
END

ACTION_DEFINE_ASSOCIATIVE_ARRAY old_to_new_eff_mapping BEGIN
  "s!mon4"=>"s!monsu4"
  "s!mon5"=>"s!monsu5"
  "s!mon6"=>"s!monsu6"
  "s!mon7"=>"s!monsu7"
END

ACTION_PHP_EACH old_to_new_eff_mapping AS orig_eff=>new_eff BEGIN
  COPY_EXISTING ~%orig_eff%.eff~ ~override/%new_eff%.eff~
    WRITE_EVALUATED_ASCII 0x30 ~%new_eff%~ #8
END

ACTION_DEFINE_ASSOCIATIVE_ARRAY spell_to_eff_mapping BEGIN
  "spwi618"=>"s!monsu4"
  "spwi712"=>"s!monsu5"
  "spwi815"=>"s!monsu6"
  "spwi902"=>"s!monsu7"
END

ACTION_PHP_EACH spell_to_eff_mapping AS summ_spell=>summ_eff BEGIN
  COPY_EXISTING ~%summ_spell%.spl~ ~override/%summ_spell%d.spl~
    WRITE_LONG 0x08 ~-1~ // Remove spell name
    LPF ~ALTER_EFFECT~
      INT_VAR
        match_opcode = 177
      STR_VAR
        resource = EVALUATE_BUFFER ~%summ_eff%~
    END
    LPF ~ALTER_EFFECT~
      INT_VAR
        match_opcode = 318
      STR_VAR
        parameter1 = 6
        parameter2 = 134
        resource = EVALUATE_BUFFER ~%summ_spell%d~
    END
    LPF ~ALTER_EFFECT~
      INT_VAR
        match_opcode = 326
        parameter1 = 6
        parameter2 = 134
    END
END

ACTION_FOR_EACH orig_summ_spell IN ~spwi618~ ~spwi712~ ~spwi815~ ~spwi902~ BEGIN
  COPY_EXISTING ~%orig_summ_spell%.spl~ ~override~
  LPF ~CLONE_EFFECT~
    INT_VAR
      match_opcode = 318
      parameter1 = 128
      parameter2 = %kit_equals_row% 
    STR_VAR
      resource = EVALUATE_BUFFER ~%orig_summ_spell%~
      insert = ~first~
  END
  LPF ~CLONE_EFFECT~
    INT_VAR
      match_opcode = 326
      parameter1 = 128
      parameter2 = %kit_equals_row% 
    STR_VAR
      resource = EVALUATE_BUFFER ~%orig_summ_spell%d~
      insert = ~first~
  END
END

//////// TRANSMUTER //////////

// Stoneskin that can be cast at others

COPY_EXISTING ~spwi408.spl~ ~override/s!stonsk.spl~
  WRITE_SHORT 0x1c 4 // Innate
  LPF ~ALTER_SPELL_HEADER~
    INT_VAR
      location = 4
      target = 1
  END
  LPF ~ALTER_EFFECT~
    INT_VAR
      match_opcode = ~-1~
      target = 2
  END

LAF ~S!ADD_CLAB_ROW~
  STR_VAR
    filename = ~CLABMA04.2da~
    row_desc = ~ALLY_STONESKIN~
    level5 = ~GA_S!STONSK~
    level15 = ~GA_S!STONSK~
    level25 = ~GA_S!STONSK~
    level35 = ~GA_S!STONSK~
END


// Tenser's Transformation

COPY ~IATweaks\resources\eff\s!1_2apr.eff~ ~override~
COPY ~IATweaks\resources\spl\spwi603d.spl~ ~override~
COPY_EXISTING ~spwi603.spl~ ~override~
  LPF ~CLONE_EFFECT~
    INT_VAR
      multi_match = 1
      match_opcode = 206
      opcode = 326
      parameter1 = 8192
      parameter2 = %kit_equals_row%
    STR_VAR
      resource = ~spwi603d~
  END
  LPF ~DELETE_EFFECT~
    INT_VAR
      match_opcode = 145
      match_parameter2 = 2
  END
  LPF ~CLONE_EFFECT~
    INT_VAR
      multi_match = 1
      match_opcode = 206
      opcode = 318
      parameter1 = 8192
      parameter2 = %kit_equals_row%
    STR_VAR
      resource = ~spwi804~
  END  
  LPF ~CLONE_EFFECT~
    INT_VAR
      multi_match = 1
      match_opcode = 206
      opcode = 318
      parameter1 = 8192
      parameter2 = %kit_equals_row%
    STR_VAR
      resource = ~spwi603~
  END  
  LPF ~CLONE_EFFECT~
    INT_VAR
      multi_match = 1
      match_opcode = 206
      opcode = 145
      parameter2 = 2
  END  

// Tenser's Partial Transformation

COPY ~IATweaks\resources\spl\spwi804d.spl~ ~override~
COPY_EXISTING ~spwi804.spl~ ~override~
  LPF ~CLONE_EFFECT~
    INT_VAR
      multi_match = 1
      match_opcode = 206
      opcode = 326
      parameter1 = 8192
      parameter2 = %kit_equals_row%
    STR_VAR
      resource = ~spwi804d~
  END

// Dragon Strength

COPY ~IATweaks\resources\bam\s!dragsb.bam~ ~override~
COPY ~IATweaks\resources\bam\s!dragsd.bam~ ~override~

OUTER_SET dragon_str_desc_str_ref = RESOLVE_STR_REF (@6057)
LAF ~S!ADD_PORTRAIT_ICON~
  INT_VAR
    description_str_ref = %dragon_str_desc_str_ref%
  STR_VAR
    bam_file = ~S!DRAGSD~
  RET
    dragon_str_icon_id = portrait_icon_id
END

COPY ~IATweaks\resources\spl\s!drgstr.spl~ ~override~
  SAY NAME1 @6057
  SAY UNIDENTIFIED_DESC @6058
  LPF ~ALTER_EFFECT~
    INT_VAR
      match_opcode = 142
      parameter2 = %dragon_str_icon_id%
  END

LAF ~S!ADD_CLAB_ROW~
  STR_VAR
    filename = ~CLABMA04.2da~
    row_desc = ~DRAGON_STRENGTH~
    level18 = ~GA_S!DRGSTR~
    level24 = ~GA_S!DRGSTR~
    level30 = ~GA_S!DRGSTR~
    level36 = ~GA_S!DRGSTR~
END


// Blinding Speed

COPY ~IATweaks\resources\bam\s!blspdb.bam~ ~override~
COPY ~IATweaks\resources\spl\s!bldspd.spl~ ~override~
  SAY NAME1 @6059
  SAY UNIDENTIFIED_DESC @6060

LAF ~S!ADD_CLAB_ROW~
  STR_VAR
    filename = ~CLABMA04.2da~
    row_desc = ~BLINDING_SPEED~
    level14 = ~GA_S!BLDSPD~
    level21 = ~GA_S!BLDSPD~
    level28 = ~GA_S!BLDSPD~
    level35 = ~GA_S!BLDSPD~
END

//////// ABJURER ////////

// Chain Sequencer

COPY_EXISTING ~inarea.pro~ ~override/s!wij01.pro~
  // READ_BYTE 0x200 pro_flags
  // WRITE_BYTE 0x200 (%pro_flags% BOR 0b00000010) // make it affect only enemies
  WRITE_SHORT 0x214 0 // No secondary projectile

ADD_PROJECTILE ~override\s!wij01.pro~

COPY ~IATweaks\resources\bam\spwij01b.bam~ ~override~
COPY ~IATweaks\resources\spl\spwij01.spl~ ~override~
  SAY NAME1 @6061
  SAY UNIDENTIFIED_DESC @6062
COPY ~IATweaks\resources\spl\spwij01d.spl~ ~override~
  SAY NAME1 @6063
  LPF ~ALTER_SPELL_HEADER~
    INT_VAR
      projectile = %s!wij01%
  END

COPY ~IATweaks\resources\spl\spwij01e.spl~ ~override~
COPY ~IATweaks\resources\spl\spwij01p.spl~ ~override~
  SAY NAME1 @6063
  LPF ~ALTER_SPELL_HEADER~
    INT_VAR
      projectile = %s!wij01%
  END

COPY_EXISTING ~bgee.lua~ ~override~
	REPLACE_TEXTUALLY EXACT_MATCH ~mageBookStrings = {~ ~mageBookStrings = {
	SPWIJ01 = {tip = 60420, title = 'MINOR_SEQUENCER_TITLE', action = "ADD_SPELLS_SEQUENCER_LABEL"},~

LAF ~S!ADD_CLAB_ROW~
  STR_VAR
    filename = ~CLABMA07.2da~
    row_desc = ~CHAIN_SEQUENCER~
    level15 = ~GA_SPWIJ01~
    level25 = ~GA_SPWIJ01~
END

// +5% elemental damage per 10 levels

COPY ~IATweaks\resources\spl\s!+5eled.spl~ ~override~

LAF ~S!ADD_CLAB_ROW~
  STR_VAR
    filename = ~CLABMA07.2da~
    row_desc = ~5%_ELEM_DAMAGE~
    level10 = ~GA_S!+5ELED~
    level20 = ~GA_S!+5ELED~
    level30 = ~GA_S!+5ELED~
END

////// Illusionist ////////

// Non-detection

// Add spellstate shield_equipped

LAF resolve_state STR_VAR new_state_id = ~NON_DETECTION~ RET new_state_index END
OUTER_SET non_detection_state = %new_state_index%

// Add 7eyes.2da file if it does not exist

ACTION_IF NOT FILE_EXISTS_IN_GAME ~7EYES.2da~ THEN BEGIN
  COPY ~IATweaks\resources\2da\7eyestempl.2da~ ~override/7EYES.2da~
END

LAF ~S!ADD_7EYES_ROW~
  INT_VAR
    strref        = RESOLVE_STR_REF (@6065)
  STR_VAR
    row_desc      = ~NON_DETECTION~
    spellstate    = EVAL ~%non_detection_state%~
    eff1          = ~136~ // Force visible
    eff2          = ~116~ // Cure invisibility
    eff3          = ~47~ // Another cure invisibility
    eff4          = ~220*5~ // Remove spell school protection (ILLUSIONIST)
    eff5          = ~221*3~ // Remove spell type protection (ILLUSIONARYPROTECTIONS)
    eff6          = ~*~
    eff7          = ~*~
    eff8          = ~*~
    eff9          = ~*~
  RET non_detect_row_id = new_row_id 
END

COPY_EXISTING ~spwi310.spl~ ~override~
  SAY UNIDENTIFIED_DESC @6064
  LPF ~ALTER_SPELL_HEADER~
    INT_VAR
      range = 60 //Visual range of the caster
  END

  LPF ~ALTER_EFFECT~
    INT_VAR
      match_opcode = 69
      opcode = 335
      parameter1 = %non_detection_state%
      parameter2 = %non_detect_row_id%
  END

// Create a list of all spells that grant Improved Invisibility
COPY_EXISTING_REGEXP GLOB ~.*\.SPL~ ~override~
  LPF ~S!SPELL_USES_OPCODE~
    INT_VAR
      match_opcode   = 20 // Invisibility
      match_param2   = 1  // Improved
    RET is_found opcode_resource
  END
  PATCH_IF ~%is_found%~ STR_EQ ~TRUE~ BEGIN
    SPRINT source_name ~%SOURCE_RES%~
    TO_UPPER source_name
    SPRINT $imp_inv_spells("%source_name%") "irrelevant"
  END
  BUT_ONLY

ACTION_PHP_EACH imp_inv_spells AS ii_spell=>irrelevant BEGIN
  OUTER_SET immune_to_ii_strref = RESOLVE_STR_REF (@6066)
  // Apply -4 penalty to Saving Throws, since Improved Invisibility grants a +4 bonus
  COPY_EXISTING ~%ii_spell%.spl~ ~override~
    LPF ~S!CLONE_EFFECT~
      INT_VAR
        match_opcode = 20
        match_parameter2 = 1
        opcode = 325
        parameter1 = ~-4~
        parameter2 = 0
    END
    // Remove all vanilla protections from II spells
    PATCH_PHP_EACH imp_inv_spells AS ii_spell_inner=>irrelevant BEGIN
      LPF ~DELETE_EFFECT~
        INT_VAR
          match_opcode = 206
        STR_VAR
          match_resource = EVAL ~%ii_spell_inner%~
      END
    // Make all II spells protect against all other such spells for their duration
      PATCH_IF ~%SOURCE_RES%~ STRING_COMPARE_CASE ~%ii_spell_inner%~ != 0 BEGIN
        LPF ~CLONE_EFFECT~
          INT_VAR
            match_opcode = 20
            opcode = 206
            multi_match = 1
            parameter1 = %immune_to_ii_strref%
            parameter2 = 0
          STR_VAR
            resource = EVAL ~%ii_spell_inner%~
            insert = ~last~
        END
      END
    END
    // Ensure that protection from self is applied as the last effect
    LPF ~CLONE_EFFECT~
      INT_VAR
        match_opcode = 20
        opcode = 206
        multi_match = 1
        parameter1 = %immune_to_ii_strref%
        parameter2 = 0
      STR_VAR
        resource = EVAL ~%SOURCE_RES%~
        insert = ~last~
    END
    BUT_ONLY
  // Make spells that remove Invisibility also dispel all such spells to ensure that -4 ST penalty is not duplicated
  ACTION_FOR_EACH spell_removing_invisibility IN ~spwi203.spl~ ~sppr309.spl~ BEGIN
    COPY_EXISTING ~%spell_removing_invisibility%~ ~override~
      LPF ~CLONE_EFFECT~
        INT_VAR
          match_opcode = 116
          opcode = 321
        STR_VAR
          resource = EVAL ~%SOURCE_RES%~
      END
    BUT_ONLY
  END
END

// Add permanent sequencers for single and multi target Non-detection to Illusionist

COPY ~IATweaks\resources\eff\s!stillu.eff~ ~override~
COPY ~IATweaks\resources\eff\s!mtillu.eff~ ~override~
COPY ~IATweaks\resources\spl\s!mtillu.spl~ ~override~
COPY ~IATweaks\resources\spl\s!stillu.spl~ ~override~
COPY ~IATweaks\resources\spl\s!illu01.spl~ ~override~
COPY ~IATweaks\resources\spl\s!illu02.spl~ ~override~

LAF ~S!ADD_CLAB_ROW~
  STR_VAR
    filename = ~CLABMA08.2da~
    row_desc = ~CONTINGENCY_NONDETECTION_SINGLE~
    level15 = ~AP_S!ILLU01~
END
LAF ~S!ADD_CLAB_ROW~
  STR_VAR
    filename = ~CLABMA08.2da~
    row_desc = ~CONTINGENCY_NONDETECTION_MULTI~
    level15 = ~AP_S!ILLU02~
END

// Add sequencer triggers to Improved and Mass Invisibility

COPY_EXISTING ~spwi405.spl~ ~override~
  LPF ~ADD_SPELL_EFFECT~
    INT_VAR
      opcode = 258
      target = 1
      parameter1 = 0
      parameter2 = 0
      resist_dispel = 0
      duration = 0
    STR_VAR
      resource = ~S!STILLU~
  END      

COPY_EXISTING ~spwi721.spl~ ~override~
  LPF ~ADD_SPELL_EFFECT~
    INT_VAR
      opcode = 260
      target = 1
      parameter1 = 0
      parameter2 = 0
      resist_dispel = 0
      duration = 0
    STR_VAR
      resource = ~S!MTILLU~
  END
      
// Simulacrum

COPY ~IATweaks\resources\bam\s!simulb.bam~ ~override~
COPY ~IATweaks\resources\bam\s!simulc.bam~ ~override~
COPY ~IATweaks\resources\spl\s!simorg.spl~ ~override~
  SAY UNIDENTIFIED_DESC @6067
COPY ~IATweaks\resources\spl\s!simuli.spl~ ~override~

COPY_EXISTING ~simulacr.spl~ ~override~
  LPF ~ADD_SPELL_EFFECT~
    INT_VAR
      opcode = 146
      target = 1
      parameter1 = 0
      parameter2 = 0
      resist_dispel = 0
      duration = 0
    STR_VAR
      resource = ~S!SIMULI~
  END

LAF ~S!ADD_CLAB_ROW~
  STR_VAR
    filename = ~CLABMA08.2da~
    row_desc = ~SIMULACRUM~
    level16 = ~GA_S!SIMORG~
END

// TODO:
// - set number of attacks to 0
// - address level properly
// - remove opcode 177 to prevent non-detection? maybe through a wizard spell that removes it if spell level < 14
//   as the 1st class can be Fighter, not Illusionist
// Add some bonuses to HiS and Detect Illusions

//////// Enchanter /////////

// Bonus Saving Throws against enchantment

COPY ~IATweaks\resources\spl\s!+1ench.spl~ ~override~

LAF ~S!ADD_CLAB_ROW~
  STR_VAR
    filename = ~CLABMA09.2da~
    row_desc = ~SAVE_VS_ENCHANTMENT~
    level6  = ~AP_S!+1ENCH~
    level12 = ~AP_S!+1ENCH~
    level18 = ~AP_S!+1ENCH~
    level24 = ~AP_S!+1ENCH~
    level30 = ~AP_S!+1ENCH~
    level36 = ~AP_S!+1ENCH~
END

// Focused Mind Blast

ADD_PROJECTILE ~IATweaks\resources\pro\s!mindbl.pro~
COPY ~IATweaks\resources\bam\s!mindbl.bam~ ~override~
COPY ~IATweaks\resources\bam\s!mblasb.bam~ ~override~
COPY ~IATweaks\resources\spl\s!mindbl.spl~ ~override~
  SAY NAME1 @6068
  SAY UNIDENTIFIED_DESC @6069

COPY ~IATweaks\resources\spl\s!mblast.spl~ ~override~
  LPF ~ALTER_SPELL_HEADER~ 
    INT_VAR 
      projectile = ~%s!mindbl%~
  END

LAF ~S!ADD_CLAB_ROW~
  STR_VAR
    filename = ~CLABMA09.2da~
    row_desc = ~FOCUSED_MIND_BLAST~
    level10 = ~GA_S!MINDBL~
    level20 = ~GA_S!MINDBL~
    level30 = ~GA_S!MINDBL~
END

// TODO:
// - decide what to do with godslaying enchantment
// - probably rename it to High Enchantment or something
// - potentially grant some bonuses to thac0/damage

/////// Diviner /////////

// Automatically learn Foreknowledge at lvl 16

LAF ~S!ADD_CLAB_ROW~
  STR_VAR
    filename = ~CLABMA05.2da~
    row_desc = ~FOREKNOWLEDGE~
    level16 = ~GA_SPWI807~
END

// See Invisible

COPY ~IATweaks\resources\bam\s!seeinb.bam~ ~override~
COPY ~IATweaks\resources\bam\s!seeind.bam~ ~override~

OUTER_SET see_invisible_desc_str_ref = RESOLVE_STR_REF (@6070)
LAF ~S!ADD_PORTRAIT_ICON~
  INT_VAR
    description_str_ref = %see_invisible_desc_str_ref%
  STR_VAR
    bam_file = ~S!SEEIND~
  RET
    see_invisible_icon_id = portrait_icon_id
END

COPY ~IATweaks\resources\spl\s!seeinv.spl~ ~override~
  SAY NAME1 @6070
  SAY UNIDENTIFIED_DESC @6071
  LPF ~ALTER_EFFECT~
    INT_VAR
      match_opcode = 142
      parameter2 = %see_invisible_icon_id%
  END

LAF ~S!ADD_CLAB_ROW~
  STR_VAR
    filename = ~CLABMA05.2da~
    row_desc = ~SEE_INVISIBLE~
    level18 = ~GA_S!SEEINV~
    level25 = ~GA_S!SEEINV~
    level32 = ~GA_S!SEEINV~
END

// Glimpse of the Future

COPY ~IATweaks\resources\2da\s!glimp1.2da~ ~override~
COPY ~IATweaks\resources\bam\s!glimpb.bam~ ~override~
COPY ~IATweaks\resources\bam\s!glimpd.bam~ ~override~

OUTER_SET glimpse_of_future_desc_str_ref = RESOLVE_STR_REF (@6072)
LAF ~S!ADD_PORTRAIT_ICON~
  INT_VAR
    description_str_ref = %glimpse_of_future_desc_str_ref%
  STR_VAR
    bam_file = ~S!GLIMPD~
  RET
    glimpse_of_future_icon_id = portrait_icon_id
END

COPY ~IATweaks\resources\spl\s!glimp1.spl~ ~override~
  SAY NAME1 @6072
  SAY UNIDENTIFIED_DESC @6073

COPY ~IATweaks\resources\spl\s!glimp2.spl~ ~override~
  SAY NAME1 @6072
  SAY UNIDENTIFIED_DESC @6073
  LPF ~ALTER_EFFECT~
    INT_VAR
      match_opcode = 142
      parameter2 = %glimpse_of_future_icon_id%
  END

COPY ~IATweaks\resources\spl\s!glimp3.spl~ ~override~

LAF ~S!ADD_CLAB_ROW~
  STR_VAR
    filename = ~CLABMA05.2da~
    row_desc = ~GLIMPSE_OF_FUTURE~
    level1  = ~GA_S!GLIMP1~
    level8  = ~GA_S!GLIMP1~
    level16 = ~GA_S!GLIMP1~
    level24 = ~GA_S!GLIMP1~
    level32 = ~GA_S!GLIMP1~
    level40 = ~GA_S!GLIMP1~
END