SILENT

INCLUDE "%MOD_FOLDER%/lib/components/IA_rebalancing/helper_functions.tph"

// Make all Cleric kits use unique files for their HLAs

ACTION_DEFINE_ASSOCIATIVE_ARRAY hla_row_to_abbrev_mapping BEGIN
  45=>"Cl1"
  46=>"Cl2"
  47=>"Cl3"
  60=>"Cl4"
  61=>"Cl5"
END

COPY_EXISTING ~luabbr.2da~ ~override~
  COUNT_2DA_COLS ~colcount~
  PHP_EACH hla_row_to_abbrev_mapping AS row => abbreviation BEGIN
    SET_2DA_ENTRY_LATER ~_#_#_#luabbr~ row 1 ~%abbreviation%~
  END
  SET_2DA_ENTRIES_NOW ~_#_#_#luabbr~ ~colcount~
  PRETTY_PRINT_2DA

// Make copies of the original Cleric HLA table

ACTION_PHP_EACH hla_row_to_abbrev_mapping AS _ => abbreviation BEGIN
  COPY_EXISTING ~lucl0.2da~ ~override/lu%abbreviation%.2da~
END

// Enable ** in Sword and Shield for Cleric and all kits and *** for Tyr
COPY_EXISTING ~weapprof.2da~  ~override/weapprof.2da~
  COUNT_2DA_COLS ~colcount~
  SET_2DA_ENTRY_LATER ~_#_#_#weapprof~ 29 6 ~2~
  SET_2DA_ENTRY_LATER ~_#_#_#weapprof~ 29 13 ~2~
  SET_2DA_ENTRY_LATER ~_#_#_#weapprof~ 29 17 ~2~
  SET_2DA_ENTRY_LATER ~_#_#_#weapprof~ 29 18 ~2~
  SET_2DA_ENTRY_LATER ~_#_#_#weapprof~ 29 20 ~2~
  SET_2DA_ENTRY_LATER ~_#_#_#weapprof~ 29 48 ~2~
  SET_2DA_ENTRY_LATER ~_#_#_#weapprof~ 29 49 ~2~
  SET_2DA_ENTRY_LATER ~_#_#_#weapprof~ 29 50 ~2~
  SET_2DA_ENTRY_LATER ~_#_#_#weapprof~ 29 62 ~2~ // Tyr
  SET_2DA_ENTRY_LATER ~_#_#_#weapprof~ 29 63 ~2~
  SET_2DA_ENTRIES_NOW ~_#_#_#weapprof~ ~colcount~
  PRETTY_PRINT_2DA

// Update class description for Clerics
COPY_EXISTING ~clastext.2da~ ~override/clastext.2da~
  COUNT_2DA_COLS ~colcount~
  // Pure Cleric class
  SET_2DA_ENTRY 23 4 ~colcount~ RESOLVE_STR_REF (@5006)

// add bonuses for *** in Sword and Shield
COPY_EXISTING ~stylbonu.2da~  ~override/stylbonu.2da~
  COUNT_2DA_COLS ~colcount~
  COUNT_2DA_ROWS ~colcount~ ~rowcount~
  INSERT_2DA_ROW ~rowcount~ ~colcount~ ~SWORDANDSHIELD-3 0                0                0                0                -3               -3               0                0~
  PRETTY_PRINT_2DA

// Add new projectiles

ADD_PROJECTILE ~IATweaks\resources\pro\s!defha1.pro~
ADD_PROJECTILE ~IATweaks\resources\pro\s!defha2.pro~

// Add condition for checking specific kit in splprot.2da

LAF ~S!ADD_SPLPROT_2DA_ENTRY~
  STR_VAR
    condition_description = ~KIT=n~
    stat                  = ~152~
    value                 = ~-1~
    relation              = ~1~
  RET
    kit_equals_row = new_line_number
END

// Rework Defensive Harmony

COPY_EXISTING ~sppr406.spl~ ~override/sppr406d.spl~
  SAY UNIDENTIFIED_DESC @6002
  LPF ~ALTER_SPELL_HEADER~
    INT_VAR
      projectile = %s!defha1% // New projectile with smaller area of effect
  END
  LPF ~DELETE_EFFECT~
    INT_VAR
      match_opcode = 321 // Shaman aura cleanse
    STR_VAR
      match_resource = ~SPPRI25~
  END
  LPF ~DELETE_EFFECT~
    INT_VAR
      match_opcode = 215 // Play visual effect
  END
  LPF ~DELETE_EFFECT~
    INT_VAR
      match_opcode = 174 // Play sound
  END
  LPF ~ALTER_EFFECT~
    INT_VAR
      match_opcode = 321
    STR_VAR
      match_resource = ~sppr406~
      resource = ~sppr406d~
  END
  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      duration_high = 6
  END
  LPF ~ADD_SPELL_EFFECT~
    INT_VAR
      opcode = 50
      target = 2
      power = 4
      parameter1 = ~-675892480~
      parameter2 = 1310720
      resist_dispel = 3
  END

COPY ~IATweaks\resources\spl\sppr406e.spl~ ~override\sppr406e.spl~ // Cleric kit only bonus spell
  LPF ~ALTER_SPELL_HEADER~
    INT_VAR
      projectile = %s!defha2% // New projectile with smaller area of effect
  END

COPY ~IATweaks\resources\spl\s!defhar.spl~ ~override\sppr406.spl~ // New Defensive Harmony spell
  LPF ~ALTER_EFFECT~
    INT_VAR
      match_opcode = 146
    STR_VAR
      resource = ~sppr406d~
  END
  PATCH_FOR_EACH kit_name IN ~OHTYR~ ~GODTALOS~ ~GODHELM~ ~GODLATHANDER~ ~OHTEMPUS~ BEGIN
    LPF ~CLONE_EFFECT~
      INT_VAR
        match_opcode = 146
        opcode = 326
        parameter1 = IDS_OF_SYMBOL (kit ~%kit_name%~)
        parameter2 = kit_equals_row
      STR_VAR
        resource = ~sppr406e~
        insert = ~below~
    END
  END

// Weaken Undead

COPY ~IATweaks\resources\eff\S!CRTHDC.EFF~ ~override~
COPY ~IATweaks\resources\eff\S!CRDMDC.EFF~ ~override~
COPY ~IATweaks\resources\eff\S!MIDMDC.EFF~ ~override~
COPY ~IATweaks\resources\eff\S!PIDMDC.EFF~ ~override~
COPY ~IATweaks\resources\eff\S!SLDMDC.EFF~ ~override~

COPY_EXISTING ~SPPR515.spl~ ~override/SPPR515.spl~
  SAY NAME1 @6003
  SAY UNIDENTIFIED_DESC @6004

  // Update duration for all effects
  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      check_globals = 0
      match_opcode = ~-1~
      duration = 36
  END

  // Update icon effect to bypass MR
  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      check_globals = 0
      match_opcode = 142
      resist_dispel = 2
  END

  // Remove original "cast spell on condition", since it's affected by slow/haste
  LPF ~DELETE_SPELL_EFFECT~
    INT_VAR
      opcode_to_delete = 232
  END

  // Make the spell apply its effects every 6s like True Sight
  PATCH_FOR_EACH ~weaken_wave~ IN "0" "1" "2" "3" "4" "5" "6" BEGIN
    LPF ~ADD_SPELL_EFFECT~ 
      INT_VAR
        opcode = 146
        target = 1
        parameter2 = 1
        resist_dispel = 2
        timing = 3
        duration = 6 * %weaken_wave%
        power = 5
      STR_VAR
        resource = ~SPPR515D~
    END
  END

// Weaken Undead subspell
COPY_EXISTING ~SPPR515D.spl~ ~override/SPPR515D.spl~
  SAY NAME1 @6003
  SAY UNIDENTIFIED_DESC @6004

  // Update duration for all effects
  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      check_globals = 0
      match_opcode = ~-1~
      duration = 38
  END

  // Remove previous instance of this spell to prevent stacking
  LPF ~ADD_SPELL_EFFECT~
    INT_VAR
      opcode = 321
      target = 2
      insert_point = 0
      resist_dispel = 3
    STR_VAR
      resource = ~SPPR515D~
  END

  // Delete Wing Buffet effect
  LPF ~DELETE_EFFECT~
    INT_VAR
      check_globals = 0
      match_opcode = 177
    STR_VAR
      match_resource = ~REPDEAD~
  END

  // Modify visual effect
  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      check_globals = 0
      match_opcode = 177
      duration = 6
      timing = 0
      resist_dispel = 3
    STR_VAR
      match_resource = ~REPVISU~
  END

  // Modify sound effect
  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      check_globals = 0
      match_opcode = 177
      resist_dispel = 3
      timing = 0
    STR_VAR
      match_resource = ~COLDHIT~
  END

  // Add weakening effects
  LPF ~CLONE_EFFECT~
    INT_VAR
      match_opcode = 177
    STR_VAR
      match_resource = ~COLDHIT~
      resource = ~S!CRTHDC~
  END

  LPF ~CLONE_EFFECT~
    INT_VAR
      match_opcode = 177
    STR_VAR
      match_resource = ~COLDHIT~
      resource = ~S!SLDMDC~
  END

  LPF ~CLONE_EFFECT~
    INT_VAR
      match_opcode = 177
    STR_VAR
      match_resource = ~COLDHIT~
      resource = ~S!CRDMDC~
  END

  LPF ~CLONE_EFFECT~
    INT_VAR
      match_opcode = 177
    STR_VAR
      match_resource = ~COLDHIT~
      resource = ~S!PIDMDC~
  END  

  LPF ~CLONE_EFFECT~
    INT_VAR
      match_opcode = 177
    STR_VAR
      match_resource = ~COLDHIT~
      resource = ~S!MIDMDC~
  END

// Remove immunity to Weaken Undead from enemies

ACTION_FOR_EACH item_with_515_immunity IN ~S!PATRIC.ITM~ ~S!SKEL.ITM~ BEGIN
  COPY_EXISTING ~%item_with_515_immunity%~ ~override~
    LPF ~DELETE_EFFECT~
      INT_VAR
        match_opcode = 206
      STR_VAR
        match_resource = ~sppr515d~
    END
END

// Protective Chant

// Add records for the new portrait icons
OUTER_SET protective_chant_desc_str_ref = RESOLVE_STR_REF (@6005)
LAF ~S!ADD_PORTRAIT_ICON~
  INT_VAR
    description_str_ref = %protective_chant_desc_str_ref%
  STR_VAR
    bam_file = ~SPPR520D~
  RET
    protective_chant_icon_id = portrait_icon_id
END
// Main spell file

COPY ~IATweaks\resources\spl\sppr520.spl~ ~override~
  SAY NAME1 @6005
  SAY UNIDENTIFIED_DESC @6006
  LPF ~ALTER_EFFECT~
    INT_VAR
      match_opcode = 142
      parameter2 = %protective_chant_icon_id%
  END

// BAMs for the file

COPY ~IATweaks\resources\bam\sppr520b.bam~ ~override~
COPY ~IATweaks\resources\bam\sppr520c.bam~ ~override~
COPY ~IATweaks\resources\bam\sppr520d.bam~ ~override~

// Add custom spells to a list of exclusion from automatically learnable spells
COPY_EXISTING ~hidespl.2da~ ~override/hidespl.2da~
  COUNT_2DA_COLS ~colcount~
  COUNT_2DA_ROWS ~colcount~ ~rowcount~
  // INSERT_2DA_ROW ~rowcount~ ~colcount~ ~SPPR445 1 0 0~
  // INSERT_2DA_ROW ~rowcount~ ~colcount~ ~SPPR446 1 0 0~
  // INSERT_2DA_ROW ~rowcount~ ~colcount~ ~SPPR447 1 0 0~
  // INSERT_2DA_ROW ~rowcount~ ~colcount~ ~SPPR448 1 0 0~
  // INSERT_2DA_ROW ~rowcount~ ~colcount~ ~SPPR449 1 0 0~
  INSERT_2DA_ROW ~rowcount~ ~colcount~ ~SPPR520 1 0 0~

ACTION_FOR_EACH cleric_kit_clab IN ~CLABPR02.2da~ ~CLABPR03.2da~ ~CLABPR04.2da~ ~OHTYR.2da~ ~OHTEMPUS.2da~ BEGIN
  LAF ~S!ADD_CLAB_ROW~
    STR_VAR
      filename = ~%cleric_kit_clab%~
      row_desc = ~PROTECTIVE_CHANT~
      level9 = ~GA_SPPR520~
  END
END

// Purify Aura

COPY ~IATweaks\resources\spl\spcl943.spl~ ~override~
  SAY NAME1 @6007
  SAY UNIDENTIFIED_DESC @6008

// BAMs for the file

COPY ~IATweaks\resources\bam\spcl943b.bam~ ~override~
COPY ~IATweaks\resources\bam\spcl943c.bam~ ~override~

// Add HLA entries

ACTION_FOR_EACH cleric_kit_hla_file IN ~lucl1.2da~ ~lucl2.2da~ ~lucl3.2da~ ~lucl4.2da~ ~lucl5.2da~ BEGIN
  LAF ~S!ADD_HLA_ROW~
    STR_VAR
      filename = ~%cleric_kit_hla_file%~
      row_desc = ~PURIFY_AURA~
      ability = ~GA_SPCL943~
      num_allowed = ~3~
  END
END

// Heavenly Shield

// Add spellstate shield_equipped

LAF resolve_state STR_VAR new_state_id = ~SHIELD_EQUIPPED~ RET new_state_index END
OUTER_SET shield_equipped_state = %new_state_index%

// Patch all shields to set shield_equipped state when worn

COPY_EXISTING_REGEXP GLOB ~.+\.itm~ ~override~
  READ_SHORT 0x1c item_category
  PATCH_IF (item_category = 12)
    THEN BEGIN
      LPF ~ADD_ITEM_EQEFFECT~
        INT_VAR
          opcode = 335
          target = 1
          parameter1 = %shield_equipped_state%
          timing = 2  
      END
    END

// Add needed splprot entries

LAF ~S!ADD_SPLPROT_2DA_ENTRY~
  STR_VAR
    condition_description = ~SHIELD_EQUIPPED~
    stat                  = ~0x112~
    value                 = ~%shield_equipped_state%~
    relation              = ~1~
  RET
    is_shield_equipped = new_line_number
END

LAF ~S!ADD_SPLPROT_2DA_ENTRY~
  STR_VAR
    condition_description = ~SWORD_SHIELD_STYLE=n~
    stat                  = ~112~
    value                 = ~-1~
    relation              = ~1~
  RET
    sw_sh_style = new_line_number
END

// Add records for the new portrait icons

OUTER_SET heavenly_shield_desc_str_ref = RESOLVE_STR_REF (@6009)
LAF ~S!ADD_PORTRAIT_ICON~
  INT_VAR
    description_str_ref = %heavenly_shield_desc_str_ref%
  STR_VAR
    bam_file = ~SPPR617D~
  RET
    heavenly_shield_icon_id = portrait_icon_id
END

// BAMs for the file

COPY ~IATweaks\resources\bam\sppr617b.bam~ ~override~
COPY ~IATweaks\resources\bam\sppr617c.bam~ ~override~
COPY ~IATweaks\resources\bam\sppr617d.bam~ ~override~

// Main spell file

COPY ~IATweaks\resources\spl\sppr617.spl~ ~override~
  SAY NAME1 @6009
  SAY UNIDENTIFIED_DESC @6010
  LPF ~ALTER_EFFECT~
    INT_VAR
      match_opcode = 142
      parameter2 = %heavenly_shield_icon_id%
  END

COPY ~IATweaks\resources\spl\SPPR617D.spl~ ~override/SPPR617D.spl~
COPY ~IATweaks\resources\spl\SPPR617E.spl~ ~override/SPPR617E.spl~
  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      check_headers = 1
      match_opcode = 326
      parameter2 = ~%is_shield_equipped%~
  END

COPY ~IATweaks\resources\spl\SPPR617F.spl~ ~override/SPPR617F.spl~
  SAY NAME1 @6011
  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      check_headers = 1
      match_opcode = 326
      parameter2 = ~%sw_sh_style%~
  END

COPY ~IATweaks\resources\spl\SPPR617G.spl~ ~override/SPPR617G.spl~
COPY ~IATweaks\resources\spl\SPPR617H.spl~ ~override/SPPR617H.spl~
COPY ~IATweaks\resources\spl\SPPR617I.spl~ ~override/SPPR617I.spl~
COPY ~IATweaks\resources\spl\SPPR617J.spl~ ~override/SPPR617J.spl~
COPY ~IATweaks\resources\eff\S!hvshld.eff~ ~override/S!hvshld.eff~

ACTION_FOR_EACH cleric_kit_clab IN ~CLABPR02.2da~ ~CLABPR03.2da~ ~CLABPR04.2da~ ~OHTYR.2da~ ~OHTEMPUS.2da~ BEGIN
  LAF ~S!ADD_CLAB_ROW~
    STR_VAR
      filename = ~%cleric_kit_clab%~
      row_desc = ~HEAVENLY_SHIELD~
      level11 = ~GA_SPPR617~
  END
END


// Strength of One

// Add exceptional strength check

LAF ~S!ADD_SPLPROT_2DA_ENTRY~
  STR_VAR
    condition_description = ~EXCEPT_STR>n~
    stat                  = ~37~
    value                 = ~-1~
    relation              = ~3~
  RET
    except_str_higher_than = new_line_number
END

COPY_EXISTING ~sppr312.spl~ ~override~
  SAY UNIDENTIFIED_DESC @6012
  LPF ~ADD_SPELL_EFFECT~
    INT_VAR
      opcode = 318
      insert_point = 0
      target = 2
      power = 3
      parameter1 = 19
      parameter2 = 124
      duration = 0
      resist_dispel = 0
    STR_VAR
      resource = "SPPR312"
    END

    LPF ~CLONE_EFFECT~
      INT_VAR
        match_opcode = 318
        parameter1 = 75
        parameter2 = %except_str_higher_than%
      STR_VAR
        insert = ~last~
    END

    LPF ~CLONE_EFFECT~
      INT_VAR
        match_opcode = 97
      STR_VAR
        insert = ~last~
    END

    LPF ~DELETE_EFFECT~
      INT_VAR
        match_opcode = 97
        multi_match = 1
    END
  BUT_ONLY

// Holy Power

COPY_EXISTING ~SPPR412.spl~ ~override/SPPR412.spl~
  SAY UNIDENTIFIED_DESC @6013

  // Remove original strength bonus
  LPF ~DELETE_SPELL_EFFECT~
    INT_VAR
      opcode_to_delete = 44
  END

  // Protect from last effect if str > 18
  LPF ~ADD_SPELL_EFFECT~ 
    INT_VAR
      opcode = 318
      insert_point = ~-1~
      target = 1
      power = 4
      parameter1 = 19
      parameter2 = 124
      duration = 1
      resist_dispel = 0
    STR_VAR
      resource = "SPPR412"
  END

  // Add strength bonus as the last effect
  LPF ~CLONE_EFFECT~
    INT_VAR
      check_globals = 0
      match_opcode = 97
      opcode = 44
      parameter1 = 18
    STR_VAR
      insert = ~last~
  END
  BUT_ONLY

// Remove Summon Animals and Poison from Clerics

COPY_EXISTING ~SPPR402.spl~ ~override/SPPR402.spl~
              ~SPPR411.spl~ ~override/SPPR411.spl~
              ~SPPR501.spl~ ~override/SPPR501.spl~
              ~SPPR602.spl~ ~override/SPPR602.spl~
              ~SPPR604.spl~ ~override/SPPR604.spl~
  WRITE_BYTE  0x21      0x40
  BUT_ONLY

// Remove those spells from known/memorized spells for all non-Druid non-Shaman characters

COPY_EXISTING_REGEXP GLOB ~.+\.cre~ ~override~
  READ_BYTE 0x0273 char_class

  // Remove summon animals spells
  PATCH_IF %char_class% != 21 AND %char_class% != 11 AND %char_class% != 16 BEGIN
    REMOVE_KNOWN_SPELL ~SPPR402~
    REMOVE_KNOWN_SPELL ~SPPR411~
    REMOVE_KNOWN_SPELL ~SPPR501~
    REMOVE_KNOWN_SPELL ~SPPR602~
    REMOVE_KNOWN_SPELL ~SPPR604~
    REMOVE_MEMORIZED_SPELL ~SPPR402~
    REMOVE_MEMORIZED_SPELL ~SPPR411~
    REMOVE_MEMORIZED_SPELL ~SPPR501~
    REMOVE_MEMORIZED_SPELL ~SPPR602~
    REMOVE_MEMORIZED_SPELL ~SPPR604~
  END
  BUT_ONLY

// Blade Barrier

// Assign new projectile that hits only enemies

COPY_EXISTING ~sppr603d.spl~ ~override~
  LPF ~ALTER_SPELL_HEADER~
    INT_VAR
      projectile = 218
  END

// Prevent haste/slow from making this spell hit more/less often

COPY_EXISTING ~sppr603.spl~ ~override~
  LPF ~CLONE_EFFECT~
    INT_VAR
      match_opcode = 232
      opcode = 321
      target = 1
      parameter2 = 0
      duration = 0
    STR_VAR
      match_resource = ~sppr603d~
      insert = ~first~
      resource = ~sppr603~
  END
  LPF ~ALTER_EFFECT~
    INT_VAR
      match_opcode = 232
      opcode = 333
      parameter1 = 11
  END
  BUT_ONLY

// Do the same for player version of Globe of Blades

COPY_EXISTING ~sppr725.spl~ ~override~
  LPF ~CLONE_EFFECT~
    INT_VAR
      match_opcode = 232
      opcode = 321
      target = 1
      parameter2 = 0
      duration = 0
    STR_VAR
      match_resource = ~sppr725d~
      insert = ~first~
      resource = ~sppr725~
  END
  LPF ~ALTER_EFFECT~
    INT_VAR
      match_opcode = 232
      opcode = 333
      parameter1 = 11
  END
  BUT_ONLY

// Cure spells

ACTION_FOR_EACH healing_spell IN ~sppr103.spl~ ~sppr315.spl~ ~sppr401.spl~ ~sppr502.spl~ BEGIN
  COPY_EXISTING ~%healing_spell%~ ~override~
    READ_LONG 0x34 spell_level
    LPF ~CLONE_EFFECT~
      INT_VAR
        match_opcode = 17
        parameter1 = %spell_level% * 5
        parameter2 = 2
    END
END

// Update spell descriptions

COPY_EXISTING ~sppr103.spl~ ~override~
  SAY UNIDENTIFIED_DESC @6014
// Update spell descriptions

COPY_EXISTING ~sppr315.spl~ ~override~
  SAY UNIDENTIFIED_DESC @6015

COPY_EXISTING ~sppr401.spl~ ~override~
  SAY UNIDENTIFIED_DESC @6016

COPY_EXISTING ~sppr502.spl~ ~override~
  SAY UNIDENTIFIED_DESC @6017

// Sunray and False Dawn

// Remove immunities to those spells from almost all items

COPY_EXISTING ~s!bodhi.itm~ ~override~
              ~s!patric.itm~ ~override~
              ~s!skel.itm~ ~override~
              ~s!vlord.itm~ ~override~
              ~gorchr.itm~ ~override~
  LPF ~DELETE_EFFECT~
    INT_VAR
      match_opcode = 206
    STR_VAR
      match_resource = ~sppr609~ // False Dawn
  END
  LPF ~DELETE_EFFECT~
    INT_VAR
      match_opcode = 206
    STR_VAR
      match_resource = ~sppr707~ // Sunray
  END

// Remove immunities to those spells from almost all creatures

COPY_EXISTING_REGEXP GLOB ~ceskel.cre~ ~override~
                          ~gorlic01.cre~ ~override~
                          ~grvlch01.cre~ ~override~
                          ~hlshade.cre~ ~override~
                          ~hlshang.cre~ ~override~
                          ~rngsha04.cre~ ~override~
                          ~s!bolich.cre~ ~override~
                          ~s!grlch[1-9].cre~ ~override~
                          ~s!glchra.cre~ ~override~
                          ~s!mflich.cre~ ~override~
                          ~s!roec01.cre~ ~override~
                          ~s!rozvan.cre~ ~override~
                          ~s!sandri.cre~ ~override~
                          ~s!schri.cre~ ~override~
                          ~s!sfort.cre~ ~override~
                          ~s!sfran.cre~ ~override~
                          ~s!shlich.cre~ ~override~
                          ~s!slawr1.cre~ ~override~
                          ~s!slesl.cre~ ~override~
                          ~s!smelb.cre~ ~override~
                          ~s!sshad[1-3].cre~ ~override~
                          ~senlich.cre~ ~override~
                          ~theshal.cre~ ~override~
  LPF ~DELETE_EFFECT~
    INT_VAR
      match_opcode = 206
    STR_VAR
      match_resource = ~sppr609~ // False Dawn
    END
  LPF ~DELETE_EFFECT~
    INT_VAR
      match_opcode = 206
    STR_VAR
      match_resource = ~sppr707~ // Sunray
  END
  BUT_ONLY
  IF_EXISTS


// False Dawn

ADD_PROJECTILE ~IATweaks\resources\pro\S!FALSED.PRO~
COPY ~IATweaks\resources\eff\s!0frres.eff~ ~override~

COPY_EXISTING ~SPPR609.spl~ ~override/SPPR609.spl~
  SAY UNIDENTIFIED_DESC @6018

  LPF ~ALTER_SPELL_HEADER~ 
    INT_VAR 
      projectile = ~%S!FALSED%~ 
      speed = 8
    END
  
  // Modify current effects to bypass magic resistance
  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      check_globals = 0
      match_opcode = 177
      resist_dispel = 2
      timing = 0
      parameter1 = 125
      parameter2 = 4
  END

  // Add another effect, which sets fire resistance to 0
  LPF ~CLONE_EFFECT~
    INT_VAR
      check_globals = 0
      match_opcode = 177
      timing = 0
    STR_VAR
      match_resource = ~DAWNDAM~
      resource = ~S!0frres~
  END

  // Clone all effects and make them work only on Shadows
  PATCH_FOR_EACH cre_class IN 132 133 134 136 175  BEGIN
    LPF ~CLONE_EFFECT~
      INT_VAR
        check_globals = 0
        match_opcode = 177
        parameter1 = %cre_class%
      STR_VAR
        match_resource = ~S!0frres~
    END

    LPF ~CLONE_EFFECT~
      INT_VAR
        check_globals = 0
        match_opcode = 177
        parameter1 = %cre_class%
      STR_VAR
        match_resource = ~DAWNDAM~
    END

    LPF ~CLONE_EFFECT~
      INT_VAR
        check_globals = 0
        match_opcode = 177
        parameter1 = %cre_class%
      STR_VAR
        match_resource = ~DAWNVIS2~
    END
  END

// Sunray

// Add condition for checking specific kit in splprot.2da

LAF ~S!ADD_SPLPROT_2DA_ENTRY~
  STR_VAR
    condition_description = ~FIRE_RES>n~
    stat                  = ~14~
    value                 = ~-1~
    relation              = ~3~
  RET
    fire_res_higher = new_line_number
END

COPY ~IATweaks\resources\eff\s!sunray.eff~ ~override~
COPY ~IATweaks\resources\eff\s!75fres.eff~ ~override~

COPY_EXISTING ~SPPR707.spl~ ~override/SPPR707.spl~
  SAY UNIDENTIFIED_DESC @6019

  // Remove level progression from the spell
  PATCH_FOR_EACH ~min_lvl_val~ IN "15" "16" "17" "18" "19" "20" BEGIN
    LPF ~DELETE_SPELL_HEADER~ 
      INT_VAR
        header_type = ~-1~
        min_level = ~%min_lvl_val%~
    END
  END

  // Change damage from magic to fire
  LPF ~ALTER_SPELL_EFFECT~ 
    INT_VAR
      match_opcode  = 12
      parameter2 = 0x00080000
  END

  // Remove original effects to undead
  LPF ~DELETE_SPELL_EFFECT~ 
    INT_VAR
      opcode_to_delete = 177
  END

  // -25% of current fire resistance to Undead
  LPF ~ADD_SPELL_EFFECT~ 
    INT_VAR
      opcode = 177
      insert_point = 0
      target = 2
      power = 7
      parameter1 = 4
      parameter2 = 3
      duration = 0
      resist_dispel = 0
    STR_VAR
      resource = "S!75fres"
  END

  // Additional damage to undead
  LPF ~ADD_SPELL_EFFECT~ 
    INT_VAR
      opcode = 177
      insert_point = 1
      target = 2
      power = 7
      parameter1 = 4
      parameter2 = 3
      timing = 1
      duration = 0
      resist_dispel = 0
    STR_VAR
      resource = "S!sunray"
  END

  // Add immunity to this spell if fire res > 100
  LPF ~ADD_SPELL_EFFECT~ 
    INT_VAR
      opcode = 318
      insert_point = 0
      target = 2
      power = 7
      parameter1 = 100
      parameter2 = %fire_res_higher%
      duration = 0
      resist_dispel = 0
    STR_VAR
      resource = "SPPR707"
  END

// Disrupt Undead

COPY_EXISTING ~sppr710.spl~ ~override/sppr710.spl~
  SAY UNIDENTIFIED_DESC @6020
  WRITE_BYTE  0x27    0xc // secondary type
  WRITE_BYTE  0x72    2 // change type to ranged
  LPF ~ALTER_SPELL_HEADER~ INT_VAR
    header = 1
    projectile = 209
    range = 15
  END

  // Protect from the spell for non-undead creatures
  LPF ~ADD_SPELL_EFFECT~ 
    INT_VAR
      opcode = 318
      insert_point = ~-1~
      target = 2
      power = 7
      parameter2 = 2
      duration = 1
      resist_dispel = 0
    STR_VAR
      resource = "sppr710"
  END

  // Direct damage opcode
  LPF ~ALTER_EFFECT~
    INT_VAR
      match_opcode = 177
      opcode = 12
      parameter1 = 30
      parameter2 = 0
      dicenumber = 10
      dicesize = 6
    STR_VAR
      match_resource = ~s!disrup~
  END

// Cause Serious/Critical Wounds
COPY ~IATweaks\resources\spl\sppr414d.spl~ ~override~
COPY ~IATweaks\resources\spl\sppr414e.spl~ ~override~
COPY ~IATweaks\resources\spl\sppr414f.spl~ ~override~
COPY ~IATweaks\resources\spl\sppr510d.spl~ ~override~
COPY ~IATweaks\resources\spl\sppr510e.spl~ ~override~
COPY ~IATweaks\resources\spl\sppr510f.spl~ ~override~

COPY_EXISTING ~SPPR414.spl~ ~override/SPPR414.spl~
  SAY UNIDENTIFIED_DESC @6021

  // Update casting speed
  LPF ~ALTER_SPELL_HEADER~ INT_VAR speed = 2 END

COPY_EXISTING ~SPPR510.spl~ ~override/SPPR510.spl~
  SAY UNIDENTIFIED_DESC @6022

  // Update casting speed
  LPF ~ALTER_SPELL_HEADER~ INT_VAR speed = 3 END


ACTION_DEFINE_ASSOCIATIVE_ARRAY cause_wounds_to_item_mapping BEGIN
  ~sppr414~=>~serious~
  ~sppr510~=>~critical~
END

ACTION_PHP_EACH cause_wounds_to_item_mapping AS spell_file => item_file BEGIN
  COPY_EXISTING ~%spell_file%.spl~ ~override~
    // Increase duration to 60s
    LPF ~ALTER_SPELL_EFFECT~
      INT_VAR
        match_opcode = 111
        duration = 60
    END
    // Add protection from item removal
    LPF ~CLONE_EFFECT~
      INT_VAR
        match_opcode = 111
        opcode = 146
        parameter2 = 1
      STR_VAR
        resource = EVAL ~%spell_file%e~
    END

  COPY_EXISTING ~%item_file%.itm~ ~override/%item_file%.itm~
    WRITE_SHORT 0x94 0 // charges
    WRITE_SHORT 0x86 4 // thaco bonus
    // bypass resistance, add strength bonuses
    LPF ~ALTER_ITEM_HEADER~
      INT_VAR
        check_headers = 1
        dicesize = 1
        dicenumber = 1
        flag_strength = 1
    END
    LPF ~ALTER_ITEM_EFFECT~
      INT_VAR
        check_headers = 1
        match_opcode = 12
        resist_dispel = 3
    END
    // add immunity to this weapon to undead
    LPF ~ADD_SPELL_EFFECT~
      INT_VAR
        insert_point = 0
        header = 1
        opcode = 324
        target = 2
        parameter2 = 1
        duration = 1
      STR_VAR
        resource = EVAL ~%item_file%~
    END
    // add immunity to this weapon to elementals
    LPF ~ADD_SPELL_EFFECT~
      INT_VAR
        insert_point = 0
        header = 1
        opcode = 324
        target = 2
        parameter2 = 9
        duration = 1
      STR_VAR
        resource = EVAL ~%item_file%~
    END
    // add immunity to this weapon to golems
    LPF ~ADD_SPELL_EFFECT~
      INT_VAR
        insert_point = 0
        header = 1
        opcode = 324
        target = 2
        parameter2 = 27
        duration = 1
      STR_VAR
        resource = EVAL ~%item_file%~
    END
    // set APR to 2,5
    LPF ~ADD_ITEM_EQEFFECT~
      INT_VAR
        type = 1
        opcode = 1
        target = 1
        timing = 2
        parameter1 = 8
        parameter2 = 1
    END
    // add opcodes for item removal after hit
    LPF ~ADD_SPELL_EFFECT~
      INT_VAR
        type = 1
        opcode = 146
        target = 1
        parameter2 = 1
        insert_point = ~-1~
      STR_VAR
        resource = EVAL ~%spell_file%d~
    END
    LPF ~ADD_SPELL_EFFECT~
      INT_VAR
        type = 1
        opcode = 146
        target = 1
        parameter2 = 1
        insert_point = ~-1~
      STR_VAR
        resource = EVAL ~%spell_file%f~
    END
  BUT_ONLY
  IF_EXISTS
END

// Slay Living

COPY ~IATweaks\resources\spl\s!slaylv.spl~ ~override~
  LPF ~ALTER_EFFECT~
    INT_VAR
      opcode = 139
      parameter1 = RESOLVE_STR_REF (@6024)
  END
COPY ~IATweaks\resources\eff\s!slaylv.eff~ ~override~

COPY_EXISTING ~sppr511.spl~ ~override~
  SAY UNIDENTIFIED_DESC @6023
  READ_BYTE 0x1e spell_flags
  WRITE_BYTE 0x1e (%spell_flags% BOR 0b000001100) // disable the spell for non-evil characters

  LPF ~ALTER_EFFECT~
    INT_VAR
      match_opcode = 111
      opcode = 248
      duration = 60
    STR_VAR
      resource = ~s!slaylv~
  END
  
  PATCH_FOR_EACH ~creature_race~ IN ~DWARF~ ~ELF~ ~GITHYANKI~ ~GNOME~ ~GOBLIN~ ~HALF_ELF~ ~HALFLING~ ~HALFORC~ ~HOBGOBLIN~ ~HUMAN~ ~ORC~ BEGIN
    SET race_val = IDS_OF_SYMBOL (race ~%creature_race%~)
    LPF ~ADD_SPELL_EFFECT~
      INT_VAR
        opcode = 178
        target = 1
        parameter1 = %race_val%
        parameter2 = 4
        resist_dispel = 3
        duration = 60
    END
    LPF ~ADD_SPELL_EFFECT~
      INT_VAR
        opcode = 179
        target = 1
        parameter1 = %race_val%
        parameter2 = 4
        resist_dispel = 3
        duration = 60
    END
  END

// Harm

// Add new projectiles

ADD_PROJECTILE ~IATweaks\resources\pro\s!harm10.pro~
ADD_PROJECTILE ~IATweaks\resources\pro\s!harm05.pro~

COPY ~IATweaks\resources\bam\s!harm.bam~ ~override~
COPY ~IATweaks\resources\vvc\s!harmex.vvc~ ~override~
COPY ~IATweaks\resources\itm\s!harm.itm~ ~override~

COPY ~IATweaks\resources\spl\s!harm10.spl~ ~override~
  LPF ~ALTER_SPELL_HEADER~
    INT_VAR
      projectile = %s!harm10%
  END

COPY ~IATweaks\resources\spl\s!harm05.spl~ ~override~
  LPF ~ALTER_SPELL_HEADER~
    INT_VAR
      projectile = %s!harm05%
  END


COPY_EXISTING ~sppr608.spl~ ~override~
  SAY UNIDENTIFIED_DESC @6025
  LPF ~ALTER_EFFECT~
    INT_VAR
      match_opcode = 111
    STR_VAR
      resource = ~s!harm~
  END

// Demon Hunt

COPY ~IATweaks\resources\eff\s!demhun.eff~ ~override~
COPY ~IATweaks\resources\bam\sppr521b.bam~ ~override~
COPY ~IATweaks\resources\bam\sppr521c.bam~ ~override~

COPY ~IATweaks\resources\spl\sppr521.spl~ ~override~
  SAY NAME1 @6028
  SAY UNIDENTIFIED_DESC @6026

COPY ~IATweaks\resources\spl\sppr521.spl~ ~override~
  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      match_opcode = 139
      parameter1 = RESOLVE_STR_REF (@6027)
  END

// Magical Stone

COPY_EXISTING ~bullet.pro~ ~override/s!mstone.pro~
  READ_BYTE 0x2c ext_flags
  WRITE_BYTE 0x2c (%ext_flags% BOR 0b00000010) // make the spell work on all creatures in its path

ADD_PROJECTILE ~override/s!mstone.pro~

COPY_EXISTING ~sppr106.spl~ ~override~
  SAY UNIDENTIFIED_DESC @6029
  LPF ~ALTER_SPELL_HEADER~
    INT_VAR
      target = 4 // Any point within range
      projectile = %s!mstone%
  END
  LPF ~ALTER_EFFECT~
    INT_VAR
      match_opcode = 12
      parameter1 = 3
  END

// Aid

COPY_EXISTING ~SPPR201.spl~ ~override/SPPR201.spl~
  SAY UNIDENTIFIED_DESC @6030

  // make the spell AoE from lvl 15
  PATCH_FOR_EACH ~spell_level~ IN "15" "16" "17" "18" "19" "20" BEGIN
    LPF ~ALTER_SPELL_HEADER~
      INT_VAR
        header = %spell_level%
        projectile = 162
    END
  END

// Greater Restoration

COPY_EXISTING ~sppr713.spl~ ~override~
  SAY UNIDENTIFIED_DESC @6031
  LPF ~ALTER_SPELL_HEADER~ INT_VAR target = 1 END
  LPF ~ALTER_EFFECT~
    INT_VAR
      match_target = 6
      target = 2
  END
  // Change fatigue to luck penalty
  LPF ~S!ALTER_EFFECT~
    INT_VAR
      match_opcode = 93
      opcode = 22
      parameter1 = ~-2~
      duration = 18
  END
  // Add fatigue icon
  LPF ~ADD_SPELL_EFFECT~
    INT_VAR
      opcode = 142
      target = 1
      parameter2 = 39
      duration = 18
  END

COPY_EXISTING ~sw1h71.itm~ ~override/sw1h71.itm~ // Hindo's Doom +4
  // Fix targeting for Greater Restoration ability
  LPF ALTER_ITEM_HEADER INT_VAR "header_type" = 3 "target" = 1 END
  BUT_ONLY

// Divine Intervention

COPY ~IATweaks\resources\bam\spcl942b.bam~ ~override~
COPY ~IATweaks\resources\bam\spcl942c.bam~ ~override~

COPY ~IATweaks\resources\spl\spcl942.spl~ ~override~
  SAY NAME1 @6032
  SAY UNIDENTIFIED_DESC @6033

ACTION_FOR_EACH cleric_kit_hla_file IN ~lucl0.2da~ ~lucl1.2da~ ~lucl2.2da~ ~lucl3.2da~ ~lucl4.2da~ ~lucl5.2da~ BEGIN
  LAF ~S!ADD_HLA_ROW~
    STR_VAR
      filename = ~%cleric_kit_hla_file%~
      row_desc = ~DIVINE_INTERVENTION~
      ability = ~GA_SPCL942~
      num_allowed = ~2~
  END
END

// Spell Duration HLA

COPY ~IATweaks\resources\bam\s!spldur.bam~ ~override~
COPY ~IATweaks\resources\spl\s!spldur.spl~ ~override~
  SAY NAME1 @6034
  SAY UNIDENTIFIED_DESC @6035


ACTION_FOR_EACH cleric_kit_hla_file IN ~lucl0.2da~ ~lucl1.2da~ ~lucl2.2da~ ~lucl3.2da~ ~lucl4.2da~ ~lucl5.2da~ BEGIN
  LAF ~S!ADD_HLA_ROW~
    STR_VAR
      filename = ~%cleric_kit_hla_file%~
      row_desc = ~LONG_SPELL_DURATION~
      ability = ~AP_s!spldur~
      num_allowed = ~1~
  END
END

// Spare the Dying

COPY ~IATweaks\resources\bam\sppr735b.bam~ ~override~
COPY ~IATweaks\resources\bam\sppr735c.bam~ ~override~
COPY ~IATweaks\resources\bam\sppr735d.bam~ ~override~

// Add records for the new portrait icons
OUTER_SET spare_the_dying_desc_str_ref = RESOLVE_STR_REF (@6036)
LAF ~S!ADD_PORTRAIT_ICON~
  INT_VAR
    description_str_ref = %spare_the_dying_desc_str_ref%
  STR_VAR
    bam_file = ~SPPR735D~
  RET
    spare_the_dying_icon_id = portrait_icon_id
END

COPY ~IATweaks\resources\spl\sppr735.spl~ ~override~
  SAY NAME1 @6036
  SAY UNIDENTIFIED_DESC @6037
  LPF ~ALTER_EFFECT~
    INT_VAR
      match_opcode = 142
      parameter2 = %spare_the_dying_icon_id%
  END

COPY ~IATweaks\resources\spl\sppr735d.spl~ ~override~
COPY ~IATweaks\resources\spl\sppr735e.spl~ ~override~
  LPF ~ALTER_EFFECT~
    INT_VAR
      match_opcode = 139
      parameter1 = RESOLVE_STR_REF (@6038)
  END

ACTION_FOR_EACH cleric_kit_clab IN ~CLABPR02.2da~ ~CLABPR03.2da~ ~CLABPR04.2da~ ~OHTYR.2da~ ~OHTEMPUS.2da~ BEGIN
  LAF ~S!ADD_CLAB_ROW~
    STR_VAR
      filename = ~%cleric_kit_clab%~
      row_desc = ~SPARE_THE_DYING~
      level14 = ~GA_SPPR735~
  END
END

// Remove Spare the Dying from resurrected characters

ACTION_FOR_EACH resurrection_spell IN ~spja01.spl~ ~sppr504.spl~ ~sppr550.spl~ ~sppr712.spl~ BEGIN
  COPY_EXISTING ~%resurrection_spell%~ ~override~
  LPF ~CLONE_EFFECT~
    INT_VAR
      match_opcode = 172
      multi_match = 1
    STR_VAR
      insert = ~last~
      resource = ~sppr735~
  END
END


// Hold Undead Lathander ability

COPY ~IATweaks\resources\eff\s!crd-10.eff~ ~override~
COPY ~IATweaks\resources\eff\s!sld-10.eff~ ~override~
COPY ~IATweaks\resources\eff\s!mvm0.eff~ ~override~
COPY ~IATweaks\resources\eff\s!mvm50.eff~ ~override~
COPY_EXISTING ~SPCL742.spl~ ~override/SPCL742.spl~
  SAY UNIDENTIFIED_DESC @6039

  // remove all but one spell ability
  PATCH_FOR_EACH ~min_lvl_val~ IN "2" "3" "4" "5" "6" "7" "8" "9" "10" "11" "12" "13" "14" "15" "16" "17" "18" "19" "20" BEGIN
    LAUNCH_PATCH_FUNCTION ~DELETE_SPELL_HEADER~ 
      INT_VAR
        header_type = ~-1~
        min_level = ~%min_lvl_val%~
    END
  END

  // change projectile to None, to make it single target
  LPF ~ALTER_SPELL_HEADER~
    INT_VAR
      projectile  = 1
  END

  // remove Hold EFF
  LPF ~DELETE_SPELL_EFFECT~
    INT_VAR
      opcode_to_delete = 185
  END

  // make all initial effects bypass MR and allow no saving throw
  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      check_globals = 0
      resist_dispel = 3
      savingthrow = 0
  END

  // Add effects slowing movements and reducing physical resistances with no saving throw
  PATCH_FOR_EACH ~eff_name~ IN "S!SLD-10" "S!CRD-10" "S!MVM50" BEGIN
    LAUNCH_PATCH_FUNCTION ~CLONE_EFFECT~ 
      INT_VAR
        timing = 0
      STR_VAR
        match_resource = ~HOLDSND~
        resource = EVALUATE_BUFFER ~%eff_name%~
    END
  END

  // Add effects slowing movements and reducing physical resistances with saving throw
  PATCH_FOR_EACH ~eff_name~ IN "S!CRD-10" "S!SLD-10" BEGIN
    LAUNCH_PATCH_FUNCTION ~CLONE_EFFECT~ 
      INT_VAR
        timing = 0
        savingthrow = 1
        savebonus = ~-2~
      STR_VAR
        match_resource = ~HOLDSND~
        resource = EVALUATE_BUFFER ~%eff_name%~
    END
  END

  // Add effects slowing movements with saving throw
  LAUNCH_PATCH_FUNCTION ~CLONE_EFFECT~ 
    INT_VAR
      savingthrow = 1
      savebonus = ~-2~
      timing = 0
    STR_VAR
      match_resource = ~HOLDSND~
      resource = "S!MVM0"
  END

  // prevent stacking
  LPF ~ADD_SPELL_EFFECT~
    INT_VAR
      opcode = 206
      target = 2
      resist_dispel = 3
      insert_point = ~-1~
    STR_VAR
      resource = ~SPCL742~
  END  

  // update duration for all effects
  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      check_globals = 0
      match_opcode = ~-1~
      duration = 18
  END

// add immunity to Hold Undead to Bodhi
COPY_EXISTING ~C6BODHI.cre~ ~override~
  LPF ~ADD_CRE_EFFECT~
    INT_VAR
      opcode = 206
      target = 1
      timing = 9
    STR_VAR
      resource = ~SPCL742~
  END

// Boon of Lathander

// Rework levels at which the ability is gained

COPY_EXISTING ~CLABPR04.2da~ ~override~
  REPLACE_EVALUATE
    ~GA_SPCL741~
    BEGIN
    END
    ~****~

LAF ~S!ADD_CLAB_ROW~
  STR_VAR
    filename = ~CLABPR04.2da~
    row_desc = ~BOON_OF_LATHANDER~
    level10 = ~GA_SPCL741~
    level16 = ~GA_SPCL741~
    level22 = ~GA_SPCL741~
    level28 = ~GA_SPCL741~
    level34 = ~GA_SPCL741~
    level40 = ~GA_SPCL741~
END

// Rework the spell itself

COPY_EXISTING ~SPCL741.spl~ ~override~
  // remove all but one spell ability
  PATCH_FOR_EACH ~min_lvl_val~ IN "11" "12" "13" "14" "15" "16" "17" "18" "19" "20" BEGIN
    LAUNCH_PATCH_FUNCTION ~DELETE_SPELL_HEADER~ 
      INT_VAR
        header_type = ~-1~
        min_level = ~%min_lvl_val%~
    END
  END

  LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ 
    INT_VAR
      match_opcode = 54
      parameter1 = 2
  END

  LAUNCH_PATCH_FUNCTION ~ALTER_EFFECT~ 
    INT_VAR
      match_opcode = 73
      parameter1 = 2
  END

  // remove immunity to level drain from the spell and move it to a subspell copy of NPP
  PATCH_FOR_EACH redundant_opcode IN 101 282 267 169 BEGIN
    LAUNCH_PATCH_FUNCTION ~DELETE_EFFECT~ 
      INT_VAR
        match_opcode = %redundant_opcode%
    END
  END
  
  LPF ~ADD_SPELL_EFFECT~
    INT_VAR
      opcode = 146
      target = 1
      parameter2 = 1
      resist_dispel = 3
      duration = 60
      power = 4
      insert_point = 3
    STR_VAR
      resource = ~SPCL741D~
  END

COPY_EXISTING ~SPPR413.spl~ ~override/spcl741d.spl~
  LPF ~ALTER_EFFECT~
    INT_VAR
      match_duration = 30
      duration = 60
  END


// Lathander Draw Upon Holy Might
COPY_EXISTING ~SPPR214.spl~ ~override/SPPR214D.spl~
  WRITE_LONG  0x08      ~-1~ // spell name
  WRITE_BYTE  0x27      0 // secondary type 

  // Remove spell states to avoid AI problems
  LPF ~DELETE_SPELL_EFFECT~
    INT_VAR
      opcode_to_delete = 328
  END

  // Make undispellable
  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      match_opcode = ~-1~
      resist_dispel = 0
  END

  // Increase duration to 90s
  LPF ~ALTER_SPELL_EFFECT~
    INT_VAR
      match_opcode = ~-1~
      duration_high = 90
  END

// Original Draw Upon Holy Might
COPY_EXISTING ~SPPR214.spl~ ~override/SPPR214.spl~
  // Protect from original spell if caster is Lathander
  LAUNCH_PATCH_FUNCTION ~ADD_SPELL_EFFECT~ 
    INT_VAR
      opcode = 318
      insert_point = ~0~
      target = 1
      power = 2
      parameter1 = 16405
      parameter2 = %kit_equals_row%
      duration = 0
      resist_dispel = 0
    STR_VAR
      resource = "SPPR214D"
  END
  // Cast improved version if caster is Lathander
  LAUNCH_PATCH_FUNCTION ~ADD_SPELL_EFFECT~ 
    INT_VAR
      opcode = 326
      insert_point = ~0~
      target = 1
      power = 2
      parameter1 = 16405
      parameter2 = %kit_equals_row%
      duration = 0
      resist_dispel = 0
    STR_VAR
      resource = "SPPR214D"
  END














// Add removal of Aura Cleansing spell from Cleric spells

// patch in removal of shaman's innate aura cleansing //

COPY_EXISTING_REGEXP GLOB ~SPPR[1-7][0-5][0-9]+\.spl~ ~override~
  READ_BYTE 0x21 exc_flags

  PATCH_IF (NOT (exc_flags BAND BIT7) = 0) BEGIN
    LPF ADD_SPELL_EFFECT
      INT_VAR
        opcode = 321
        target = 1
        insert_point = 0
      STR_VAR
        resource = ~SPPRI25~
    END
  END

  BUT_ONLY



COPY_EXISTING_REGEXP GLOB ~SPWI[1-9][0-9][0-9]+\.spl~ ~override~
                          ~SPDR201.spl~ ~override~
                          ~SPDR603.spl~ ~override~
                          ~SPDR702.spl~ ~override~
                          ~SPDWD02.spl~ ~override~
                          ~SPCL121.spl~ ~override~
                          ~SPCL144.spl~ ~override~
                          ~SPCL152.spl~ ~override~
                          ~SPCL311.spl~ ~override~
                          ~SPCL321.spl~ ~override~
                          ~SPCL342.spl~ ~override~
                          ~SPCL412.spl~ ~override~
                          ~SPCL414.spl~ ~override~
                          ~SPCL417.spl~ ~override~
                          ~SPCL423.spl~ ~override~
                          ~SPCL52[12]+\.spl~ ~override~
                          ~SPCL61[1-3]+\.spl~ ~override~
                          ~SPCL62[1-5]+\.spl~ ~override~
                          ~SPCL63[2-4]+\.spl~ ~override~
                          ~SPCL64[134]+\.spl~ ~override~
                          ~SPCL72[12]+\.spl~ ~override~
                          ~SPCL73[12]+\.spl~ ~override~
                          ~SPCL74[12]+\.spl~ ~override~
                          ~SPCL90[0-9]+\.spl~ ~override~
                          ~SPCL91[0-4]+\.spl~ ~override~
                          ~SPCL91[6-9]+\.spl~ ~override~
                          ~SPCL92[12]+\.spl~ ~override~
                          ~SPCL93[6-8]+\.spl~ ~override~
                          ~SPCL94[0-3]+\.spl~ ~override~
                          ~SPCLI13.spl~ ~override~
                          ~SPIN001.spl~ ~override~
                          ~SPIN1[01][0-9].spl~ ~override~
                          ~SPIN12[0-7].spl~ ~override~
                          ~SPIN15[0-7].spl~ ~override~
                          ~SPIN649.spl~ ~override~
                          ~SPIN667.spl~ ~override~
                          ~SPIN71[378].spl~ ~override~
                          ~SPIN82[6-9].spl~ ~override~
                          ~SPIN853.spl~ ~override~
                          ~SPRA30[1-6].spl~ ~override~
                          ~SPSD02.spl~ ~override~
                          ~SPINI101.spl~ ~override~
                          ~SPINI2[67].spl~ ~override~
                          ~SPINI9[0-2].spl~ ~override~
                          ~OHTMPS[0-9]+\.spl~ ~override~
                          ~OHTYR[0-9]+\.spl~ ~override~

  LPF ADD_SPELL_EFFECT
    INT_VAR
      opcode = 321
      target = 1
      insert_point = 0
    STR_VAR
      resource = ~SPPRI25~
  END

// add custom spells to a list of exclusion from automatically learnable spells
COPY_EXISTING ~hidespl.2da~ ~override/hidespl.2da~
  COUNT_2DA_COLS ~colcount~
  COUNT_2DA_ROWS ~colcount~ ~rowcount~
  INSERT_2DA_ROW ~rowcount~ ~colcount~ ~SPPR617 1 0 0~
  INSERT_2DA_ROW ~rowcount~ ~colcount~ ~SPPR735 1 0 0~
  PRETTY_PRINT_2DA